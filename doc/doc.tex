\documentclass{article}
    \usepackage[margin=0.7in]{geometry}
    \usepackage[parfill]{parskip}
    \usepackage[utf8]{inputenc}

    % linguagem:
    \usepackage[brazil]{babel}
    \usepackage{csquotes}

    % bibtex
    \usepackage{biblatex}
    \bibliography{doc}

    \usepackage{blindtext, hyperref}
    \usepackage{amsmath,amssymb,amsfonts,amsthm, mathtools}
    \usepackage{listings,xcolor,caption}

    \newcommand{\divides}{\mid}
    \newcommand{\notdivides}{\nmid}

    \definecolor{codegreen}{rgb}{0,0.6,0}
    \definecolor{codegray}{rgb}{0.5,0.5,0.5}
    \definecolor{codepurple}{rgb}{0.58,0,0.82}
    \definecolor{backcolour}{rgb}{0.95,0.95,0.92}

    \lstdefinestyle{mystyle} {
        backgroundcolor=\color{backcolour},
        commentstyle=\color{codegreen},
        keywordstyle=\color{magenta},
        numberstyle=\tiny\color{codegray},
        stringstyle=\color{codepurple},
        basicstyle=\ttfamily\footnotesize,
        breakatwhitespace=false,
        breaklines=true,
        captionpos=b,
        keepspaces=true,
        numbers=left,
        numbersep=5pt,
        showspaces=false,
        showstringspaces=false,
        showtabs=false,
        tabsize=2,
        escapeinside={tex(}{tex)}
    }
    \lstset{style=mystyle}
    \renewcommand{\lstlistingname}{Algoritmo}

    \DeclarePairedDelimiter{\ceil}{\lceil}{\rceil}

    \title{Trabalho 02 - Álgebra A}
    \author{Luis Higino, Mateus Vitor}

\begin{document}

\maketitle

\section{Introdução}
\label{intro}
O presente documento tem como objetivo explicar a implementação dos algoritmos utilizados para resolver os seguintes problemas:
\begin{itemize}
    \item Resolver sistemas de equações lineares sobre o $\mathbb{Z}_2$.
    \item Encontrar as soluções da congruência quadrática $x^2 \equiv n \pmod{p}$.
    \item Fatorar um número $N \gg 0$ em dois fatores não triviais.
\end{itemize} 
\section{Desenvolvimento}

O trabalho foi desenvolvido na linguagem \href{https://www.haskell.org/}{Haskell}, utilizando do tipo padrão \verb| Integer | para armazenar inteiros de precisão arbitrária. O código fonte do trabalho pode ser encontrado neste repositório do GitHub:
\\\verb|https://github.com/mateusvmv/tp2_algebra_a|.

\section{Módulos}

Os módulos do código fonte são

\begin{itemize}
\item Matrix: Implementa o algoritmo de resolução de sistemas lineares no $\mathbb{Z}_2$.
\item Numbers: Implementa os algoritmos de teoria dos números, como o gerador de primos, o algoritmo de Tonelli-Shanks, etc.
\item Sieve: Implementa o algoritmo do Crivo Quadrático para fatoração.
\item Main: O módulo principal do programa, que utiliza de todos os acima.
\end{itemize}

\section{Formatos de Entrada e Saída}

\subsection{Entrada}

A entrada é lida da entrada padrão \verb|stdin|. Ou seja, após rodar o programa o usuário digita com o teclado um inteiro positivo $N \gg 0$.

\subsection{Saída}

A saída do programa informa:

\begin{enumerate}
    \item O limite de fatoração $B$.
    \item A quantidade de primos utilizada na fatoração $B$-smooth.
    \item Dois inteiros $x, y$ tais que $x \not\equiv y \pmod{N}$ e $x^2 \equiv y^2 \pmod{N}$.
    \item Dois fatores não triviais de $N$.
\end{enumerate}

O programa iterativamente tenta fatorar o número com limites de fatorações diferentes, a saída do programa informa ao usuário as tentativas feitas e seus resultados até que uma resulte em uma fatoração não trivial bem sucedida.

\section{Utilização do Programa}

Para utilizar o programa, primeiro instale o GHC. Caso o sistema seja Windows ou Linux, siga \href{https://www.haskell.org/downloads/}{esse tutorial}.

Com o GHC instalado, abra um terminal no diretório raiz do código fonte e execute:

\verb|ghc -O3 Main -isrc|

Agora, basta executar o binário \verb|Main| e em seguida digitar o inteiro de entrada, seguido por um \textit{Enter}.

\section{Análises de Complexidade}

Muitos dos algoritmos, especialmente os do módulo |Numbers|, foram reaproveitados do Trabalho Prático 1. Logo, vamos discutir aqui apenas os algoritmos implementados exclusivamente para este trabalho.

\subsection{Algoritmo de Tonelli-Shanks}
\label{tonelliShanks}

O algoritmo de \textit{Tonelli-Shanks} tem como objetivo encontrar as 2 soluções para a equação
\begin{equation}
    x^2 \equiv n \pmod{p}
\end{equation}

Para isso, ele utiliza do \textbf{Critério de Euler}. Ele diz que $n$ tem uma raiz quadrada módulo $p$ se e somente se:

\begin{equation}
    n^{\frac{p-1}{2}} \equiv 1 \pmod{p}
\end{equation}

Além disso, o critério diz que quando $n$ não possui uma raiz quadrada módulo $p$, a seguinte equação vale:
\begin{equation}
    n^{\frac{p-1}{2}} \equiv -1 \pmod{p}
\end{equation}

O algoritmo então procede da seguinte forma:
\begin{enumerate}
    \item Encontra $S$ e $Q$ tais que $p-1 = 2^S \cdot Q$ em $O(\log p)$.
    \item Encontra um $z$ tal que $z^{\frac{p-1}{2}} \equiv -1 \pmod{p}$ em $O(\log p)$.
    \item Iterativamente encontra a "ordem" (é considerado apenas expoentes potências de 2) de $n$ no grupo multiplicativo de $\mathbb{Z} / \mathbb{Z}_p$.
\end{enumerate}

A complexidade final do pior caso do algoritmo é $O(\log^4 p)$, mas o número esperado de iterações são apenas 2, logo a complexidade esperada é apenas $O(\log p)$~\cite{Koo_Jo_Kwon_2013}.

\begin{minipage}{.9\linewidth}
\begin{lstlisting}[language=haskell,caption=Tonelli-Shanks]
tonelli :: Integer -> Integer -> Maybe (Integer, Integer)
tonelli n p
    | p <= 2 = Just (mod n 2, mod n 2)
    | legendre n p == 1 = Just (r, p - r)
    | otherwise = Nothing
    where
    pow = powMod p
    (s, q) = justFind (odd . snd) $ iterate (bimap (+1) (.>>. 1)) (0, p-1)
    z = (+ 1) . last $ takeWhile (\i -> legendre i p + 1 /= p) [1..p-1]
    initial = (s, pow z q, pow n $ (q+1) .>>. 1, pow n q)
    step (m, c, r, t) = (i, c', r', t') where
        i = fst . justFind ((==1) . snd) $ iterate (bimap (+ 1) (`pow` 2)) (0, t)
        b = pow c (shiftL 1 (m - i - 1))
        r' = mod (r*b) p
        c' = mod (b*b) p
        t' = mod (t*c') p
    condition (_, _, _, t) = mod (t - 1) p == 0
    (_,_,r,_) = justFind condition (iterate step initial)

\end{lstlisting}
\end{minipage}

\subsection{Resolução de Sistemas Lineares}
\label{matrixSolver}

Para a resolução de Sistemas Lineares, foi implementado o algoritmo da Eliminação Gaussiana para matrizes com entradas no $\mathbb{Z}_2$.

A complexidade aritmética desse algoritmo é $O(n^3)$~\cite{Farebrother_1988}, onde $n$ é a quantidade de variáveis do sistema.
Isso ocorre pois a cada passo da eliminação, é necessário percorrer toda a matriz para zerar os elementos abaixo do pivô.
Como as entradas são binárias, as operações aritméticas do algoritmo são de fato constantes e a complexidade acima é a complexidade de tempo total.

\begin{minipage}{.9\linewidth}
\begin{lstlisting}[language=haskell,caption=Eliminação Gaussiana]
upperEchelon :: UArray (Int, Int) Bool -> UArray (Int, Int) Bool
upperEchelon mat = listArray ((0, 0), (lines, cols)) (concatMap elems . reverse $ psList) where
    (lines, cols) = snd $ bounds mat
    lineList = [listArray (0, cols) [mat!(i,j) | j <- [0..cols]] | i <- [0..lines]]
    findPivot [] _ = (Nothing, [])
    findPivot (r:rs) k
        | r!k = (Just r, rs)
        | otherwise = (r', r:mat') where (r', mat') = findPivot rs k
    takePivot p k r = if r!k then listArray (0, cols) [xor (r!j) (p!j) | j <- [0..cols]] else r
    nextPivot :: ([UArray Int Bool], [UArray Int Bool]) -> Int -> ([UArray Int Bool], [UArray Int Bool])
    nextPivot (lines, ps) k = case findPivot lines k of
        (Just p, mat') -> (map (takePivot p k) mat', p:ps)
        (Nothing, mat') -> (mat', array (0, cols) []:ps)
    (_, psList) = foldl nextPivot (lineList, []) [0..cols]

solve :: UArray (Int, Int) Bool -> UArray Int Bool
solve mat = trace ("Resolvendo matriz de tamanho " ++ show (snd $ bounds mat)) s where
    ue = upperEchelon mat
    (lines, cols) = snd $ bounds ue
    solveLine :: UArray Int Bool -> Int -> UArray Int Bool
    solveLine s i
        -- Unbound pivot or even amount of set variables, ignore
        | not (ue!(i,i)) || even = s
        -- The pivot is set because it must complement the set variables and result in zero
        | otherwise = s // [(i, True)]
        where even = foldl xor True [ue!(i,j) && s!j | j <- [i+1..cols]]
    unbound = filter (not . (ue!) . \i -> (i, i)) [0..min lines cols]
    start = array (0, cols) $ map (,True) unbound
    s = foldl solveLine start (reverse [0..min lines cols])

\end{lstlisting}
\end{minipage}

\subsection{Crivo Quadrático}
\label{quadraticSieve}
O algoritmo do \textbf{Crivo Quadrático} utiliza de um crivo para marcar números da forma:
$$ x^2 = n \bmod p \implies x^2 - n = 0 \bmod p \implies (x^2 - n) \divides p $$
Para vários primos pequenos $p$ e suas potências, o crivo armazena seus logaritmos em cada posição que satisfaça à equação acima, e são filtrados como candidatos os valores cuja soma dos logaritmos dos primos adicionados atinja $\log (x^2 - n)$, o que nos dá uma alta confiança de que $x^2 - n$ será composto apenas desses primos pequenos.

Em nossa implementação, nós realizamos diversas tentativas, com valores crescentes de $B$, até que uma encontre um fator não-trivial de $n$. Em cada tentativa, são gerados números primos menores que $B$, e filtramos os primos para os quais $n$ é resíduo quadrático. Esses primos serão utilizados para gerar candidatos para a \textbf{Fatoração de Dixon}, e então podemos utilizar o resultado disso no \textbf{Método de Fermat}.

A complexidade do algoritmo é da ordem de $O(\exp(\sqrt{\ln n \ln \ln n}))$, bem similar à equação utilizada para obter $B = \exp(\sqrt{0.5 \ln n \ln \ln n})$. Isto é porque a complexidade dominante no algoritmo é a resolução das matrizes com \textbf{Eliminação de Gauss}, e isso tem complexidade $O(B^3)$.

\begin{minipage}{0.9\linewidth}
\begin{lstlisting}[language=haskell,caption=Crivo Quadrático - Principal]
quadraticSieveAttempt n bound
    | (a /= 1 && a /= n) || (b /= 1 && b /= n) = Just ((a, b), (x, y))
    | otherwise = Nothing
  where
    primes = bPrimes n (toInteger bound)
    candidates = quadraticSieveCandidates n primes
    (x, y) = dixon n bound candidates
    (a, b) = fermatMethod n x y
\end{lstlisting}
\end{minipage}

\subsection{Crivo dos Candidatos}
\label{sieve}
Para gerar os candidatos para a \textbf{Fatoração de Dixon}, nós geramos os índices para cada primo, que são usados em um crivo segmentado, que trabalha com $2^{14}$ elementos por vez.

Para cada primo, nós encontramos a solução para $x^2 = n \bmod p$ e a estendemos para suas potências enquanto $p^k < B$.
Então, para cada um desses primos e suas potências, nós geramos listas de índices da forma $\ceil{\sqrt{n}} \leq x + py < n$. Para todos os primos e potências de primo ímpares, existem duas soluções para $x$, e as listas repetem até $n-1$.

Note que a igualdade $x^2 = n \bmod p$ é dada pelo algoritmo de \textbf{Tonelli Shanks}, e para as potências de primos $p^{k+1}$ basta verificar se $x p^k + y = n \bmod p^{k+1}$, com $y^2 = n \bmod p^k$, para todo $x$, em $O(p)$.

Nossa implementação busca por mais relações que primos na matriz, para garantir que haverá pivô livre na \textbf{Eliminação de Gauss}, e portanto solução não-trivial, ao menos na eliminação, assim o número de relações geradas é da ordem de $O(\pi(B))$.

No código abaixo, são geradas as potências de primos, as soluções das equações, os índices e então uma lista que se repete até $n$, com segmentos de tamanho $2^{14}$. Como a linguagem não é estrita, essa lista de segmentos é gerada à medida que ela é consumida, e portanto a complexidade desse algoritmo também é da ordem de $O(\pi(B))$.

\begin{minipage}{0.9\linewidth}
\begin{lstlisting}[language=haskell,caption=Crivo Quadrático - Candidatos]
quadraticSieveCandidates n iPrimes = candidates where
    r = ceilSqrt n
    len = shiftL 1 14
    indices :: (Integer, (Integer, Integer)) -> [(Integer, Double)]
    indices (p, (ia, ib)) = map (,logBase 2 $ fromInteger p) $ merge a b where
        m = p - mod r p
        repeat i = [r + i, r + i + p .. n-1]
        [a, b] = map (repeat . (`mod` p) . (+m)) [ia, ib]
    primePowers' p (ia, ib) = catMaybes
        . takeWhile (maybe False ((<maximum iPrimes) . fst))
        $ iterate (upliftTonelli n p) (Just (p, (ia, ib)))
    primePowers p = maybe [] (primePowers' p) (tonelli n p)
    solutions = concatMap primePowers iPrimes
    step (_, (i, r)) = (sfs, (i', r')) where
        sfs = quadraticSieveSeg n (concat i'') len r
        r' = r + len
        (i'', i') = unzip $ map (span ((<r') . fst)) i
    candidates = concatMap fst
        . takeWhile ((<n+len) . snd . snd)
        . iterate step
        $ ([], (map indices solutions, r))
\end{lstlisting}
\end{minipage}

Já no código abaixo, nós calculamos o segmento do crivo em si. Isto é, para cada índice calculado anteriormente que entre no segmento, nós somamos o logaritmo de seu fator primo, e para aqueles números no segmento cuja soma de logaritmos atinja o logaritmo de seu quadrado menos n nós temos grande confiança que este será composto por fatores primos pequenos. Nós também mantemos números quadrados perfeitos como candidatos, já que esses nos garantem uma solução instantânea sem precisar de passar pelos algoritmos de matriz.

\begin{minipage}{0.9\linewidth}
    \begin{lstlisting}[language=haskell,caption=Crivo Quadrático - Segmento]
    quadraticSieveSeg n indices len r = trace ("Segmento do crivo de " ++ show r ++ " a " ++ show (r+len-1) ++ " com " ++ show (length sfs) ++ " candidatos encontrados") sfs where
        logs :: UArray Integer Double
        logs = accumArray (+) 0 (r, r+len-1) indices
        condition a l = b /= 0 && (isSquare b || l  >= logB) where
            b = mod (a^2) n
            logB = logBase 2 . fromInteger $ (a^2 - n)
        sfs = map fst
            . filter (uncurry condition)
            $ assocs logs
    \end{lstlisting}
    \end{minipage}    

\subsection{Fatoração de Dixon}
\label{dixon}
O algoritmo de \textbf{Fatoração de Dixon} estende o \textbf{Método de Fermat} ao construir dois números $x^2 = y^2 \mod n$ a partir de vários números cujo quadrado módulo n possui apenas fatores primos pequenos.

Estes números com fatores primos pequenos são combinados para gerar um quadrado perfeito. Para isso, são arranjados em uma matriz com os expoentes de cada fator primo nas células, cada coluna sendo uma relação, e cada linha sendo um fator primo. Queremos que os fatores primos tenham expoente par, igual a $0 \bmod 2$, assim nós utilizamos da \textbf{Eliminação de Gauss} para encontrar as colunas da matriz que combinadas atingem esse resultado.

O algoritmo abaixo recebe uma lista de candidatos cujo quadrado módulo n pode ser B-smooth, e retorna $(x, y)$, com $x^2 = y^2$ mod n e $x \neq y$ mod n. Pode acontecer de ele encontrar um quadrado perfeito dentre os candidatos, e nesse caso ele não utiliza matriz, mas retorna o número e seu quadrado.

Como a complexidade da resolução da matriz é cúbica, e nós temos aproximadamente a mesma quantidade de linhas e colunas, isto é, igual a $\pi(B)$, a quantidade de primos menores que $B$, a complexidade final do algoritmo é $O(\pi(B)^3)$.
\begin{minipage}{0.9\linewidth}
\begin{lstlisting}[language=haskell,caption=Fatoração de Dixon]
dixon n b candidates = (x, y) where
    factorize = factorizeBSmooth $ toInteger b
    maybeFactors i = if product fs == j then Just (i, fs) else Nothing
        where j = mod (i*i) n; fs = factorize j
    countFactors = map (\l -> (fromInteger . head $ l, length l)). group
    zeroIndex :: [e] -> Array Int e
    zeroIndex l = listArray (0, length l - 1) l

    (roots, factors) = bimap zeroIndex zeroIndex
        . unzip
        . take (length smoothPrimes + 20)
        . map (second countFactors)
        $ mapMaybe maybeFactors candidates
    smoothPrimes = takeWhile (<=b) . map fromInteger $ primes

    primeIdx :: UArray Int Int
    primeIdx = array (0, b) (zip smoothPrimes [0..])

    lines = length smoothPrimes - 1
    cols = length roots - 1

    maybeOddExp s (p, k) = if odd k then Just ((primeIdx ! p, s), True) else Nothing
    matrix :: UArray (Int, Int) Bool
    matrix = array ((0, 0), (lines, cols))
        . concatMap catMaybes
        . zipWith (map . maybeOddExp) [0 .. cols]
        $ elems factors

    solution = solve matrix
    included = case findIndex (all (even . snd)) (elems factors) of
        Just i -> [i]
        Nothing -> filter (solution!) [0 .. cols]

    factorCount :: UArray Int Int
    factorCount = accumArray (+) 0 (0, lines) includedFactors where
        includedFactors = map (first (primeIdx !))
            $ concatMap (factors !) included
    mergedFactors = map (first (smoothPrimes !!)) $ assocs factorCount

    x = productMod n $ map (roots !) included
    y = productMod n
        . map (uncurry (powMod n) . bimap toInteger (.>>. 1))
        $ mergedFactors    
\end{lstlisting}
\end{minipage}

\newpage
\section{Referências}
\printbibliography


\end{document}